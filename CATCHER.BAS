1 REM DEFINT A-Z
2 REM Z = PAD(12)
3 REM I = PAD(13) -127 + I
4 REM IF PAD(13) < 128 THEN I = I + PAD(13) ELSE I = I - (255 - PAD(13))
5 REM IF PAD(13) > 127 THEN I = I - (255 - PAD(13))
7 REM PRINT I
8 REM GOTO 2
9 OPEN "GRP:" FOR OUTPUT AS #1
10 ' --- Slot 0
20 ' color 14
30 DATA &H03,&H03,&H0F,&H0F,&H0F,&H3F,&H3F,&HFF
40 DATA &HFF,&HC0,&HC0,&HC0,&HC0,&HC0,&HC0,&HC0
50 DATA &HC0,&HC0,&HF0,&HF0,&HF0,&HFC,&HFC,&HFF
60 DATA &HFF,&H03,&H03,&H03,&H03,&H03,&H03,&H03
70 ' 
80 ' --- Slot 1
90 ' color 14
100 DATA &H03,&H03,&H0F,&H0F,&H0F,&H3F,&H3F,&HFF
110 DATA &HFF,&HC0,&HE0,&H70,&H38,&H1C,&H0E,&H00
120 DATA &HC0,&HC0,&HF0,&HF0,&HF0,&HFC,&HFC,&HFF
130 DATA &HFF,&H03,&H07,&H0E,&H1C,&H38,&H70,&H00
140 ' 
150 ' --- Slot 2
160 ' color 4
170 DATA &H07,&H09,&H11,&H14,&H10,&H11,&H71,&HF0
180 DATA &HF0,&H70,&H10,&H10,&H10,&H0F,&H0C,&H12
190 DATA &HE0,&H90,&H88,&H28,&H08,&H08,&H8E,&H0F
200 DATA &H0F,&H0E,&H08,&H08,&H08,&HF0,&H30,&H48
210 ' 
220 ' --- Slot 3
230 ' color 11
240 DATA &H07,&H09,&H11,&H14,&H10,&H11,&H71,&HF0
250 DATA &HF0,&H70,&H10,&H10,&H10,&H0F,&H0C,&H12
260 DATA &HE0,&H90,&H88,&H28,&H08,&H08,&H8E,&H0F
270 DATA &H0F,&H0E,&H08,&H08,&H08,&HF0,&H30,&H48
280 ' 
290 ' --- Slot 4
300 ' color 1
310 DATA &H00,&H00,&H00,&H01,&H0F,&H3F,&H7F,&H7F
320 DATA &H7F,&H7F,&H7F,&H7F,&H7F,&H3F,&H3F,&H0F
330 DATA &H78,&HC7,&H80,&HC0,&HF0,&HF8,&HF8,&HFC
340 DATA &HFC,&HFC,&HFC,&HFC,&HFC,&HFC,&HF8,&HF8
341 DATA *
342 SCREEN 2:COLOR 15,1,15
343 TIME = 0
344 IF TIME < 10 THEN GOTO 344
347 rem BLOAD"TITLE.SC2",S
348 LINE (0,0)-STEP(256,8),1,BF
349 LOCATE 0,0: PRINT "_NetNomad and Maddie Present...":CMD SETFNT 1,1
350 LINE (16,104)-STEP(127,80),1,BF
351 LOCATE 18,106:PRINT "CONTROLLER:"
352 LOCATE 18,126:PRINT "1. PADDLE"
353 LOCATE 18,136:PRINT "2. JOYPAD"
354 LOCATE 18,146:PRINT "3. KEYBOARD"
355 LOCATE 18,156:PRINT "4. MOUSE (MSX2)"
356 LOCATE 18,176:PRINT "SELECT TO BEGIN"
357 DEFINT X:REM CONTROLLER TYPE
358 Y$=INKEY$
359 IF Y$="1" THEN X = 1: GOTO 436
360 IF Y$="2" THEN X = 2: GOTO 436
361 IF Y$="3" THEN X = 3: GOTO 436
362 IF Y$="4" THEN X = 4: GOTO 436
363 GOTO 358
370 REM IF X = 1 THEN GOTO 436 ELSE PRINT "NOT YET SUPPORTED!"
435 REM GOTO 358
436 DEFINT A:REM SPRITE 1 X 
437 DEFINT B:REM SPRITE 2 X
438 DEFINT C:REM SPRITE 3 X
439 DEFINT D:REM SPRITE 4 X
440 DEFINT E:REM SPRITE 5 X
441 DEFINT F:REM SPRITE 6 X
442 DEFINT I:REM CLAW X
443 DEFINT J:REM CLAW Y
444 DEFINT K:REM PREVIOUS CLAW Y
445 DEFINT L:REM BUTTON PRESS FLAG
447 DEFINT M:REM SPRITE NUM TEMP
448 DEFINT N:REM SCORE
449 DEFINT O:REM REMAINING DROPS FLAG
450 DEFINT Z:REM TEMP STORAGE
452 SCREEN 2,2,0:CMD SETFNT 1
453 COLOR 15,1,1:CLS
454 GOSUB 10000
455 A = 85
460 B = 170
470 C = 255
480 D = 85
490 E = 170
500 F = 255
515 I = 123
530 J = 40
545 N = 0
547 O = 10
566 N=0
571 ON SPRITE GOSUB 10030
572 BLOAD"PENGUIN.SC2",S
573 COLOR 15
574 LOCATE 200,0:PRINT #1,"TRIES:"
575 LOCATE 208,8:PRINT #1,O
579 LOCATE 0,0:PRINT #1,"SCORE:":LOCATE 8,8:PRINT #1,N
580 S = 5
581 SPRITE ON
582 TIME = 0
590 PUT SPRITE 0,(I,J),14,0
600 PUT SPRITE 1,(A,170),11,2
610 PUT SPRITE 2,(B,170),1,4
620 PUT SPRITE 3,(C,170),1,4
630 PUT SPRITE 4,(D,130),5,2
640 PUT SPRITE 5,(E,130),5,2
650 PUT SPRITE 6,(F,130),1,4
670 REM I = PDL(1)
671 IF X = 1 THEN I = PDL(1)
672 IF X = 2 THEN IF STICK(1) = 3 THEN I = I + 5 ELSE IF STICK(1) = 7 THEN I = I - 5
673 IF X = 3 THEN IF STICK(0) = 3 THEN I = I + 5 ELSE IF STICK(0) = 7 THEN I = I - 5
674 IF X = 4 THEN Z = PAD(12):IF PAD(13) < 128 THEN I = I + PAD(13) ELSE I = I - (255 - PAD(13))
675 IF I < 20 THEN I = 20
677 IF I > 225 THEN I = 225
680 IF A < 255 THEN A = A + S + S ELSE A = 0
690 IF B < 255 THEN B = B + S + S ELSE B = 0
700 IF C < 255 THEN C = C + S + S ELSE C = 0
710 IF D > -1 THEN D = D - S ELSE D = 255
720 IF E > -1 THEN E = E - S ELSE E = 255
730 IF F > -1 THEN F = F - S ELSE F = 255
735 K = J
740 IF J > 180 THEN GOTO 9100
741 Z = 0
742 IF X =1 THEN IF PDL(9) = 0 THEN Z = Z + 1
743 IF STRIG(0) = -1 THEN z = Z + 1
744 IF STRIG(1) = -1 THEN z = Z + 1
745 IF (Z > 0) AND (L = 0) THEN L = 1: O = O - 1:LINE (208,8)-(250,16),1,BF:COLOR 15:LOCATE 208,8:PRINT #1,O:BEEP:PLAY "","l32o4v11 bb-aa-gg-fee-dd-co3bb-aa-gg-fee-dd-c":Z = PAD(12)
750 IF L = 1 THEN J = J + 10
9000 IF TIME > 1 GOTO 582 ELSE 9000
9100 SPRITE OFF
9124 BEEP
9125 PLAY "","v10 o5 g8e16a16g8e8","v10 o4 g8e16a16g8e8"
9150 IF J > 40 THEN J = J - 1:PUT SPRITE 0,(I,J),14,0:GOTO 9150
9200 SPRITE ON
9210 IF O = 0 THEN GOTO 30000
9211 REM IF O = 0 THEN CALL WAIT(60):GOTO 30000
9250 L = 0
9300 GOTO 590 
10000 REM -- LOAD SPRITES
10010 S=BASE(9)
10020 READ R$: IF R$="*" THEN RETURN ELSE VPOKE S,VAL(R$):S=S+1:GOTO 10020
10030 IF K > J THEN RETURN ELSE SPRITE OFF
10035 IF J < 130 THEN SPRITE ON:RETURN
10045 COLOR 1
10046 LINE (8,8)-(50,16),1,BF
10049 BEEP
10050 SPRITE OFF
10051 IF K > 140 THEN GOTO 10052 ELSE GOTO 10062
10052 IF (ABS(B-I) > ABS(A-I)) AND (ABS(C-I) > ABS(A-I)) THEN M = 1:N = N + 500
10053 IF (ABS(A-I) > ABS(B-I)) AND (ABS(C-I) > ABS(B-I)) THEN M = 2
10054 IF (ABS(A-I) > ABS(C-I)) AND (ABS(B-I) > ABS(C-I)) THEN M = 3
10055 GOTO 10100
10062 IF (ABS(E-I) > ABS(D-I)) AND (ABS(F-I) > ABS(D-I)) THEN M = 4:N = N + 200
10063 IF (ABS(D-I) > ABS(E-I)) AND (ABS(F-I) > ABS(E-I)) THEN M = 5:N = N + 200
10064 IF (ABS(D-I) > ABS(F-I)) AND (ABS(E-I) > ABS(F-I)) THEN M = 6
10100 IF (M = 2 OR M = 3) OR M = 6 THEN RETURN 29999
10101 LOCATE 8,8:COLOR 15
10102 PRINT #1,N
10103 IF M = 1 THEN PLAY "","v10o5 g16o6 c16e16g8e16g4","v10o4 g16o5 c16e16g8e16g4" ELSE PLAY "","v10o4 g16o5 c16e16g8e16g4","v10o3 g16o4 c16e16g8e16g4"
10105 PUT SPRITE 0,(I,J),14,0
10110 IF M = 1 THEN PUT SPRITE M,(I,J+15),11,2 ELSE PUT SPRITE M,(I,J+15),5,2
10120 IF J > 40 THEN J = J - 1:GOTO 10105
10130 L = 0
10134 IF O = 0 THEN RETURN 30100
10135 REM IF O = 0 THEN CALL WAIT(60):RETURN 30000
10136 PUT SPRITE M,(I,150),0,2
10140 SPRITE ON:RETURN
29999 GOTO 30000
30000 SOUND 7,&B10000111
30002 SOUND 8,15:Z=1500
30003 SOUND 6,31
30004 REM SOUND 12,&O020: SOUND 14,&O070
30005 REM SOUND 15,0:TIME=0
30008 IF TIME < 40 THEN Z = Z - 1: SOUND 8,Z/100:GOTO 30008
30009 SOUND 7,&B10111111
30010 REM SOUND 6, 31
30020 REM Z=15:SOUND 8,15
30030 REM TIME = 0
30031 REM IF TIME > 10 THEN SOUND 7,&B10111111 ELSE Z=Z-1:SOUND 8,Z:GOTO 30031
30050 REM CALL WAIT(30)
30100 COLOR 15:SCREEN 1
30200 PRINT "=GAME OVER="
30350 PRINT ""
30400 PRINT "SCORE:"
30500 PRINT N
30550 IF N > P THEN P = N:PRINT "NEW HIGH SCORE:" ELSE PRINT "HIGH SCORE:"
30600 IF N = 5000 THEN PRINT "PERFECT SCORE!"
30700 PRINT P
30800 PRINT ""
30833 PRINT "P E N G U I N   C A T C H E R"
30900 PRINT "CREATED BY _NETNOMAD"
30901 PRINT "AND MADDIE"
30902 PRINT ""
30903 PRINT "MSXBAS2ROM CREATED BY AMAURY CARVALHO"
30905 PRINT ""
30906 PRINT "TINYSPRITE AND MSX SCREEN"
30907 PRINT "CONVERTER CREATED BY JANNONE"
31000 PRINT ""
31100 PRINT "PRESS FIRE TO PLAY AGAIN"
31050 IF X = 1 THEN IF (PDL(9) = 255) AND (STRIG(0) = 0) THEN GOTO 31200
31200 IF STRIG(0) = 0 THEN GOTO 31200
31250 SCREEN 2:CMD SETFNT 1
31260 L = 0
31300 GOTO 455
31400
31500 RETURN
